SUBROUTINE PARTITION_K(EXTICK,LAI,MAXLAI,FCLIMAT,FWATER,FNITRO,fswtd,ISTRESS,FSH,FRT,FLV,FST,FSO)
!-------------------------------------------------------------------------------
!THIS SUBROUTINE IS DEVELOPED ACCORDING TO DR. TAO LI'S EARLY WORK IN K-MODEL
!IT RESPONSE TO CLIMATIC INFLUENCE, LIGHT COMPETITION, WATER AND
!NITROGEN STRESS
!DATE: APRIL 29, 2010, BY DR. TAO LI
!THE FSH+FRT = 1.0 AND FST+FLV+FGR = FSH
!-------------------------------------------------------------------------------
!VARIABLE		TYPE			MEANINGS																					UNIT
!EXTICK			REAL			CANOPY LIGHT EXTINCTION COEFFICIENT								-
!LAI				REAL			LEAF AREA INDEX																		-
!MAXLAI     REAL      MAXIMUM LEAF AREA FOR SHADING TOLERANCE           -
!FCLIMAT		REAL			THE INFLUNCE DEGREE OF CLIMATE CONDITION (0 TO 1) -
!FWATER			REAL			WATER STRESS (0 TO 1)															-
!FNITRO			REAL      NITROGEN STRESS (0 TO 1)
!fswtd		REAL		the maximum osmatic potential of plant													-
!FSH				REAL 			FRACTION OF CARBON TO STEM(0 TO 1)								-
!FRT				REAL      FRACTION OF CARBON TO ROOT (0 TO 1)               -	
!FLV				REAL      FRACTION OF CARBON TO LEAF (0 TO 1)               -	
!FST				REAL      FRACTION OF CARBON TO STEM (0 TO 1)               -	
!FSO				REAL      FRACTION OF CARBON TO GRAIN (0 TO 1)              -
!FLIGHT     REAL      THE COMPETITION FACTOR TO LIGHT										-
!ISTRESS    INTEGER   THE DETERMINER FOR WHETHER THE STRESS WILL AFFCT ALLOCATION OF 
!                       CARBON AMONG ABOVE GROUND ORGANS, 1=YES,0=NO
!--------------------------------------------------------------------------------
implicit none
REAL EXTICK,LAI,MAXLAI,FCLIMAT,FWATER,FNITRO,FSWTD,FSH,FRT,FLV,FST,FSO, FLIGHT
INTEGER ISTRESS
REAL TMPV1, tmpS, tmpL
SAVE
		FLIGHT =MIN(1.0, EXP(EXTICK*(MAXLAI-LAI)))
		FNITRO = min(1.0,fnitro)							!AT THIS STAGE, THE NITROGEN STRESS WAS NOT INVOLVED YET
		fwater=min(1.0,fwater)
	 IF(FCLIMAT.LE.0.0) FCLIMAT = 1.0
	 IF(((FLV+FST+FSO).LT.0.99).OR.((FLV+FST+FSO).GT.1.01)) THEN
	  	 CALL FATALERR ('PARTITION','THE SUM OF FRACTIONS AT ABOVE GROUND ORGANS IS NOT 1.0!')
	 ENDIF
	 	!IF EITHER LIGHT COMPETITION, OR WATER STRESS OR NITROGEN STRESS EXISTED, THE MORE
	 	!CARBON WILL BE ASIGNED TO ROOT
	 	tmps=1.0-(1.0-min(fnitro, fwater))**(FSWTD*1.5)   !4.0
	 	tmpl=1.0-(1.0-flight)**4.0
   	FSH = FSH /(1.0+FCLIMAT*(1.0-tmps))
   	FRT = 1.0 - FSH
   IF(((FSH+FRT).GT.1.01).OR.((FSH+FRT).LT.0.99)) THEN
	 	 CALL FATALERR ('PATITION','THE SUM OF FRACTIONS TO ROOT ANS SHOOT IS NOT 1.0!')
	 ENDIF
   	!THE DISTRIBUTION OF CARBON AMONG ABOVE GROUND ORGANS
   TMPV1 = FST + FSO
   IF(TMPV1.LE.0.0) THEN
   	 CALL FATALERR ('PATITION','THE SUM OF FRACTIONS TO STEM AND GRAIN IS 0.0!')
   ENDIF
   IF(ISTRESS.EQ.1) THEN
   	!OPTION ONE: SUPPOSED THE EITHER THE STRESS OF WATER OR NITROGEN WILL REDUCE THE CARBON TO GRAIN AND SHIFT TO STEM
   	FST = (FST + FST/TMPV1*FCLIMAT*(1.0-tmpl)+FCLIMAT*(1.0-tmps))/ &
   				(1.0+FCLIMAT*(2.0-tmpl-tmps))
   	FLV = FLV/(1.0+FCLIMAT*(2.0-tmpl-tmps))
   	FSO = (FSO+FSO/TMPV1*FCLIMAT*(1.0-tmpl))/(1.0+FCLIMAT*(2.0-tmpl-tmps))
   ELSE
   	!OPTION TWO: SUPPOSED THE WATER OR NITROGEN STRESS DOES NOT CHANGE THE DISTRIBUTION IN ABOVE ORGANS
   	FST=(FST+FST/TMPV1*FCLIMAT*(1.0-tmpl))/(1.0+FCLIMAT*(1.0-tmpl))
   	FLV = FLV/(1.0+FCLIMAT*(1.0-tmpl))
   	FSO = (FSO+FSO/TMPV1*FCLIMAT*(1.0-tmpl))/(1.0+FCLIMAT*(1.0-tmpl))
   ENDIF
   
END SUBROUTINE